
import random

phi = 90
alpha = 32
beta  = 20
step = 0.3
stepleaf = 0.28
leaftime = 120
budtime = 20

def Start():
    # random.seed(23)
	pass

Axiom: _(0.3)@GsG(1)A(1.0)

derivation length: 400
production:

A(k) :
    p1 = min(1,(2*k+1)/k**2)
    r = random.random()
    if r <= p1:
        produce /(phi)[+(alpha)/(90);k(step)][+(alpha)D(0,k)]-(beta)D(0,k)
    else:
        produce /(phi)[+(alpha)/(90);k(step)]-(beta)D(0,k)

D(20,k) --> C(step,k)
D(x,k) --> D(x+1,k)
C(x,k) :
    if x >= 1:
        if k < 19:
            produce F(x,0.05+0.25/k)A(k+1)
        else:
            produce F(x,0.05+0.25/k)
    else:
        produce C(x+step,k)

k(s):
    if s >= 1.2:
        produce m(0,s)
    else:
        produce k(s+stepleaf)

m(120,s) --> 
m(t,s) --> m(t+1,s)

homomorphism:

C(x,k):
    d = 1
    if k > 1:
        d = 0.05+0.25* ((x/k)+(1-x)/(k-1))
    else:
        d = (0.3*(1-x))+((0.05+0.25/k)*x)
    produce F(x, d)

k(s) --> ~l(s)
m(t,s) --> ~l(s)
